buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        gradlePluginPortal()
        maven {
            url "http://nexus.twilightcity.net:8081/repository/public"
        }
    }
    dependencies {
        classpath "net.twilightcity:gradle-core:2.+"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

allprojects {
    apply plugin: "java-library"
    apply plugin: "maven-publish"
    apply plugin: "net.twilightcity.core"

    version = "${springBootVersion}-${version}"

    ext {
        groovyVersion = "2.4.20"
        spockVersion = "1.3-groovy-2.4"
    }

    repositories {
        mavenCentral()
        maven {
            url "http://nexus.twilightcity.net:8081/repository/public"
        }
    }

    dependencies {
        api 'com.google.guava:guava:31.0.1-jre'
        api "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
        api "org.springframework.boot:spring-boot-starter-actuator:${springBootVersion}"
        api("com.github.dozermapper:dozer-core:6.5.2") {
            exclude group: "org.slf4j"
        }

        sharedTestApi "net.twilightcity:common-test:1.+"
        sharedTestApi "org.codehaus.groovy:groovy-all:${groovyVersion}"
        sharedTestApi 'cglib:cglib-nodep:2.2.2'
        sharedTestApi 'org.objenesis:objenesis:1.3'
        sharedTestApi "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
        sharedTestApi "org.codehaus.groovy:groovy-all:${groovyVersion}"
        sharedTestApi ("org.spockframework:spock-core:${spockVersion}") {
            exclude group: "org.codehaus.groovy"
        }
        sharedTestApi ("org.spockframework:spock-spring:${spockVersion}") {
            exclude group: "org.codehaus.groovy"
        }
    }



    project.afterEvaluate {
        publishing {
            repositories {
                maven {
                    url "http://nexus.twilightcity.net:8081/repository/public"
                    credentials {
                        username "ci"
                        password findProperty('nexus.password')
                    }
                }
            }

            publications {
                maven(MavenPublication) {
                    artifactId = project.ext.artifactId
                    from components.java
                    artifact sourcesJar
                    artifact javadocJar

                pom {
                    name = 'Common SpringBoot'
                    description = 'A shared library providing common functionality for building SpringBoot applications, with focus on persistence operations and REST service support.'
                    url = 'https://github.com/twilightcity-net/common-spring-boot'

                    licenses {
                        license {
                            name = 'Apache License, Version 2.0'
                            url = 'https://www.apache.org/licenses/LICENSE-2.0'
                        }
                    }

                    developers {
                        developer {
                            id = 'twilightcity'
                            name = 'Twilight City, Inc'
                            email = 'admin@twilightcity.net'
                        }
                    }

                    scm {
                        connection = 'scm:git:git://github.com/twilightcity-net/common-spring-boot.git'
                        developerConnection = 'scm:git:ssh://git@github.com:twilightcity-net/common-spring-boot.git'
                        url = 'https://github.com/twilightcity-net/common-spring-boot'
                    }
                }
            }

        }
}
    }
}
